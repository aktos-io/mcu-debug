target remote localhost:4242
monitor halt

define get-breakpoints
    # generate hardcoded breakpoints
    shell make gen-breakpoints
    delete
    source breakpoints.txt
end

define cheatsheet
    shell make debug-cmd-help
end

define reload
    dont-repeat
    echo --------------------------------\n
    echo +++ compiling code...\n
    echo --------------------------------\n
    shell make; echo "set \$retval = $?" > /tmp/gdb-retval
    source /tmp/gdb-retval
    if $retval >  0
        echo --------------------------------\n
        echo !!! compilation failed!\n
        echo --------------------------------\n
    else
        echo --------------------------------\n
        echo +++ flashing the binary...\n
        echo --------------------------------\n
        # for the application code
        load ./build/ch.elf

        # for the symbol table
        file ./build/ch.elf

        # put a temporary breakpoint to the main() function
        echo ----------------------------------------\n
        echo ... 'reload' when source code is changed\n
        echo ... 'cheatsheet' for cheatsheet\n
        echo ----------------------------------------\n
        tb main
        cont

    end
end


define reset
    monitor reset
end

define exit
    kill
    quit
end

define curr
    frame
    list
end
document curr
    Show the code around currently hit breakpoint
end


# --- initialize ---
reload
reset
