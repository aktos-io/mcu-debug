set pagination off

define connect
    target extended-remote localhost:4242
end

define ub
    update-breakpoints
end
document ub
    Short for "update-breakpoints"
end

define update-breakpoints
    shell make gen-breakpoints --no-print-directory
    delete
    source _breakpoints.txt
end
document update-breakpoints
    Generate hardcoded breakpoints by using "// debugger" lines
end

define wwindow
    shell touch watch-window.txt
    echo Watch Window: \n
    echo --------------------------------\n
    source watch-window.txt
end
document wwindow
    Get pre-defined variable contents from watch-window.txt
end

define cheatsheet
    shell make debug-with-cmd-help --no-print-directory
end

define restart-server
    echo FORCE RESTARTING GDB SERVER\n
    disconnect
    shell make stop-gdb-server --no-print-directory
    # gdb server will restart by itself in 1 second
    shell sleep 3s
    connect
end

define reload
    dont-repeat
    echo --------------------------------\n
    echo +++ compiling code...\n
    shell make --no-print-directory; echo "set \$retval = $?" > /tmp/gdb-retval
    source /tmp/gdb-retval
    if $retval >  0
        echo !!! compilation failed!\n
        echo --------------------------------\n
    else
        echo +++ flashing the binary...\n
        # for the application code
        load ./build/ch.elf

        # for the symbol table
        file ./build/ch.elf

        echo done.\n

        # put a temporary breakpoint to the main() function
        echo ----------------------------------------\n
        echo ... 'reload' when source code is changed\n
        echo ... 'update-breakpoints' ('ub') to re-generate breakpoints from "// debugger" lines\n
        echo ... 'cheatsheet' for cheatsheet\n
        echo ----------------------------------------\n
        update-breakpoints
        tb main
        cont
    end
end


define reset
    monitor halt
    monitor reset
end

define exit
    kill
    quit
end

define curr
    dont-repeat
    # frame
    # list

    layout src
    focus cmd
end
document curr
    Show the code around currently hit breakpoint
end


# --- initialize ---
connect
reload
