set pagination off

define connect
    target extended-remote localhost:4242
end

define ub
    update-breakpoints
end
document ub
    Short for "update-breakpoints"
end

define update-breakpoints
    shell make gen-breakpoints --no-print-directory
    delete
    source _breakpoints.txt
end
document update-breakpoints
    Generate hardcoded breakpoints by using "// debugger" lines
end

define wwindow
    shell touch watch-window.txt
    echo Watch Window: \n
    echo --------------------------------\n
    source watch-window.txt
end
document wwindow
    Get pre-defined variable contents from watch-window.txt
end

define cheatsheet
    close-curr
    shell make debug-with-cmd-help --no-print-directory
end

define restart-server
    echo FORCE RESTARTING GDB SERVER\n
    disconnect
    shell make stop-gdb-server --no-print-directory
    # gdb server will restart by itself in 1 second
    shell sleep 3s
    connect
end

define reload
    dont-repeat
    close-curr
    echo --------------------------------\n
    echo +++ compiling code...\n
    shell make --no-print-directory; echo "set \$retval = $?" > /tmp/gdb-retval
    source /tmp/gdb-retval
    if $retval >  0
        echo !!! compilation failed!\n
        echo --------------------------------\n
    else
        echo +++ flashing the binary...\n
        # for the application code
        load ./build/ch.elf

        # for the symbol table
        file ./build/ch.elf

        echo done.\n

        # send a hard reset signal through NRST pin
        reset-mcu

	# initialize breakpoints
        update-breakpoints
        tb main	
        break _unhandled_exception

        # display TUI
        #curr

        echo ----------------------------------------\n
        echo ... 'reload' when source code is changed\n
        echo ... 'update-breakpoints' ('ub') to refresh the breakpoints\n
        echo ... 'curr'/'close-curr' to show/hide the Terminal UI. \n
        echo ... 'cheatsheet' for cheatsheet\n
        echo ----------------------------------------\n
        cont
    end
end


define reset-mcu
    monitor halt
    monitor reset
    monitor jtag_reset # send NRST signal, see https://github.com/texane/stlink/issues/774
end
document reset-mcu
    Resets MCU state by sending physical NRST signal
end

define exit
    kill
    quit
end

define curr
    dont-repeat
    # frame
    # list

    layout src
    focus cmd

    echo ------------------------------------------\n
    echo Close above TUI by "close-curr" command. \n
    echo ------------------------------------------\n
end
document curr
    Show the code around currently hit breakpoint
end

define close-curr
    tui disable
end
document close-curr
    Close the TUI display which is opened by "curr" command.
end

# --- initialize ---
connect
reload
